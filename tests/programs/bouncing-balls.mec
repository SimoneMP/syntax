# Bouncing Balls

Define the environment
  #html/event/click = [x: 0 y: 0]
  #ball = [x: 15m y: 9m vx: 40m/s vy: 9m/s]
  #system/timer = [resolution: 15ms]
  #gravity = 2m/s^2
  #boundary = 5000m

## Update condition

Now update the block positions
  ~ #system/timer.tick
  #ball.x := #ball.x + #ball.vx
  #ball.y := #ball.y + #ball.vy
  #ball.vy := #ball.vy + #gravity

## Boundary Condition

Keep the balls within the y boundary
  ~ #ball.y
  iy = #ball.y > #boundary
  #ball.y{iy} := #boundary
  #ball.vy{iy} := -#ball.vy * 80%

Keep the balls within the x boundary
  ~ #ball.x
  ix = #ball.x > #boundary
  ixx = #ball.x < 0m
  #ball.x{ix} := #boundary
  #ball.x{ixx} := 0m
  #ball.vx{ix | ixx} := -#ball.vx * 80%

## Create More Balls

Create ball on click
  ~ #html/event/click.x
  #ball += [x: 10m y: 10m vx: 40m/s vy: 0m]