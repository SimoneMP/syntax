# Bouncing Balls

Define the environment
  #html/event/click = [|x y|]
  #ball = [|x y vx vy|
            0 0 0 0]
  #system/timer = [resolution: 15, tick: 0]
  #gravity = 1
  #boundary = [x: 500 y: 500]

## Update condition

Update the block positions on each tick of the timer
  ~ #system/timer.tick
  #ball.x := #ball.x + #ball.vx
  #ball.y := #ball.y + #ball.vy
  #ball.vy := #ball.vy + #gravity

## Boundary Condition

Keep the balls within the boundary height
  iy = #ball.y > #boundary.y
  #ball.y{iy} := #boundary.x
  #ball.vy{iy} := -#ball.vy * 0.80

Keep the balls within the boundary width
  ix = #ball.x > #boundary.x
  ixx = #ball.x < 0
  #ball.x{ix} := #boundary.y
  #ball.x{ixx} := 0
  #ball.vx{ix | ixx} := -#ball.vx * 0.80

## Create More Balls

Create ball at click point
  x = #html/event/click.x
  y = #html/event/click.y
  #ball += [x: x, y: y, vx: 30, vy: 0]

## Drawing

Add the balls as to the canvas
  x = #ball.x
  y = #ball.y
  elements = [|type     cx cy radius x y stroke fill     |
               "circle" x  y  10     _ _ _      "#000000"]
  #html/canvas = [height: #boundary.x width: #boundary.y contains: [elements]]