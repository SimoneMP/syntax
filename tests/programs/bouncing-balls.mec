# Bouncing Balls

Define the environment
  #html/event/click = [|x y|]
  range = 1:5
  x = range * 30
  v = x * 0
  #ball = [|x y vx vy| x x v v]
  #system/timer = [resolution: 15, tick: 0]
  #gravity = 1
  #html/canvas = [height: 500 width: 500]

## Update condition

Update the block positions on each tick of the timer
  ~ #system/timer.tick
  #ball.x := #ball.x + #ball.vx
  #ball.y := #ball.y + #ball.vy
  #ball.vy := #ball.vy + #gravity

## Boundary Condition

Keep the balls within the canvas height
  ~ #system/timer.tick
  iy = #ball.y > #html/canvas.height
  #ball.y{iy} := #html/canvas.height
  #ball.vy{iy} := -#ball.vy * 0.80

Keep the balls within the canvas width
  ~ #system/timer.tick
  ix = #ball.x > #html/canvas.width
  ixx = #ball.x < 0
  #ball.x{ix} := #html/canvas.width
  #ball.x{ixx} := 0
  #ball.vx{ix | ixx} := -#ball.vx * 0.80

## Create More Balls

Create ball at click point
  ~ #html/event/click.x
  x = #html/event/click.x
  y = #html/event/click.y
  #ball += [x: x, y: y, vx: 30, vy: 0]