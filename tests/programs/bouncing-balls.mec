# Bouncing Balls

Define the environment
  #html/event/click = [x: 0 y: 0]
  x = 1:10
  xx = x * 20
  vy = x * 0
  vx = x * 0 + 10
  #ball = [|x y vx vy| xx xx vx vy]
  #system/timer = [resolution: 15, tick: 0]
  #gravity = 2
  #boundary = 420

## Update condition

Now update the block positions
  ~ #system/timer.tick
  #ball.x := #ball.x + #ball.vx
  #ball.y := #ball.y + #ball.vy
  #ball.vy := #ball.vy + #gravity

## Boundary Condition

Keep the balls within the y boundary
  ~ #system/timer.tick
  iy = #ball.y > #boundary
  #ball.y{iy} := #boundary
  #ball.vy{iy} := -#ball.vy

Keep the balls within the x boundary
  ~ #system/timer.tick
  ix = #ball.x > #boundary
  ixx = #ball.x < 0
  #ball.x{ix} := #boundary
  #ball.x{ixx} := 0
  #ball.vx{ix | ixx} := -#ball.vx

## Create More Balls

Create ball on click
  ~ #html/event/click.x
  #ball += [x: 10 y: 10 vx: 40 vy: 0]